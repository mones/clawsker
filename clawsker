#!/usr/bin/perl -w
#
# Clawsker :: A Claws Mail Tweaker
# Copyright 2007-2018 Ricardo Mones <ricardo@mones.org>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# See COPYING file for license details.
# See AUTHORS file for a complete list of contributors.
#

binmode STDOUT, ":encoding(utf8)";

use 5.010_000;
use strict;
use utf8;
use version 0.77;
use Glib qw(TRUE FALSE);
use Gtk3;
use File::Which;
use File::Spec::Functions;
use POSIX qw(setlocale);
use Locale::gettext;
use Encode;
use Digest::MD5 qw(md5_hex);
use Getopt::Long;

my $NAME = 'clawsker';
my $PREFIX = '@PREFIX@';
my $LIBDIR = '@LIBDIR@';
my $DATADIR = '@DATADIR@';
my $VERSION = '@VERSION@';
my $VERBOSE = FALSE;
my $READONLY = FALSE;
my $CLAWSV = undef;
my $main_window = undef;

my $locale = (defined($ENV{LC_MESSAGES}) ? $ENV{LC_MESSAGES} : $ENV{LANG});
$locale = "C" unless defined($locale);
setlocale (LC_ALL, $locale);
bindtextdomain ($NAME, catdir ($PREFIX, 'share', 'locale'));
textdomain ($NAME);

sub _ {
    my $str = shift;
    my %par = @_;
    my $xla = gettext ($str);
    if (scalar(keys(%par)) > 0) {
        foreach my $key (keys %par) {
            $xla =~ s/\{$key\}/$par{$key}/g;
        }
    }
    return decode_utf8($xla);
}

# default messages
%xl::s = (
    about_title => _('Clawsker :: A Claws Mail Tweaker'),

    l_oth_use_dlg => _('Use detached address book edit dialogue'),
    h_oth_use_dlg => _('If true use a separate dialogue to edit a person\'s details. Otherwise will use a form embedded in the address book\'s main window.'),
    l_oth_max_use => _('Maximum memory for message cache'),
    l_oth_max_use_units => _('kilobytes'),
    h_oth_max_use => _('The maximum amount of memory to use to cache messages, in kilobytes.'),
    l_oth_min_time => _('Minimun time for cache elements'),
    l_oth_min_time_units => _('minutes'),
    h_oth_min_time => _('The minimum time in minutes to keep a cache in memory. Caches more recent than this time will not be freed, even if the memory usage is too high.'),
    l_oth_use_netm => _('Use NetworkManager'),
    h_oth_use_netm => _('Use NetworkManager to switch offline automatically.'),
    l_oth_mp_rounds => _('Rounds for PBKDF2 function'),
    h_oth_mp_rounds => _('Specify the number of iterations the key derivation function will be applied on master passphrase computation. Does not modify currently stored passphrase, only master passphrases computed after changing this value are affected.'),

    l_gui_b_unread => _('Show unread messages with bold font'),
    h_gui_b_unread => _('Show unread messages in the Message List using a bold font.'),
    l_gui_no_markup => _('Don\'t use markup'),
    h_gui_no_markup => _('Don\'t use bold and italic text in Compose dialogue\'s account selector.'),
    l_gui_dot_lines => _('Use dotted lines in tree view components'),
    h_gui_dot_lines => _('Use the old dotted line look in the main window tree views (Folder, Message and other lists) instead of the modern lineless look.'),
    l_gui_h_scroll => _('Enable horizontal scrollbar'),
    h_gui_h_scroll => _('Enable the horizontal scrollbar in the Message List.'),
    l_gui_swp_from => _('Display To column instead From column in Sent folder'),
    h_gui_swp_from => _('Display the recipient\'s email address in a To column of the Sent folder instead of the originator\'s one in a From column.'),
    l_gui_v_scroll => _('Folder List scrollbar behaviour'),
    h_gui_v_scroll => _('Specify the policy of vertical scrollbar of Folder List: show always, automatic or hide always.'),
    l_gui_v_scroll_show => _('Show always'),
    l_gui_v_scroll_auto => _('Automatic'),
    l_gui_v_scroll_hide => _('Hide always'),
    l_gui_from_show => _('From column displays'),
    h_gui_from_show => _('Selects the data displayed in the From column of the Message List: name, address or both.'),
    l_gui_from_show_name => _('Name only'),
    l_gui_from_show_addr => _('Address only'),
    l_gui_from_show_both => _('Both name and address'),
    l_gui_strip_off => _('Coloured lines contrast'),
    h_gui_strip_off => _('Specify the value to use when creating alternately coloured lines in tree view components. The smaller the value, the less visible the difference in the alternating colours of the lines.'),
    l_gui_cursor_v => _('Show cursor'),
    h_gui_cursor_v => _('Display the cursor in the Message View.'),
    l_gui_toolbar_d => _('Detachable toolbars'),
    h_gui_toolbar_d => _('Show handles in the toolbars.'),
    l_gui_strip_all => _('Use stripes in all tree view components'),
    h_gui_strip_all => _('Enable alternately coloured lines in all tree view components.'),
    l_gui_strip_sum => _('Use stripes in Folder List and Message List'),
    h_gui_strip_sum => _('Enable alternately coloured lines in Message List and Folder List.'),
    l_gui_two_line_v => _('2 lines per Message List item in 3-column layout'),
    h_gui_two_line_v => _('Spread Message List information over two lines when using the three column mode.'),
    l_gui_margin_co => _('Show margin'),
    h_gui_margin_co => _('Shows a small margin in the Compose View.'),
    l_gui_mview_date => _('Don\'t display localized date'),
    h_gui_mview_date => _('Toggles localization of date format in Message View.'),
    l_gui_zero_char => _('Zero replacement character'),
    h_gui_zero_char => _('Replaces \'0\' with the given character in Folder List.'),
    l_gui_type_any => _('Editable headers'),
    h_gui_type_any => _('Allows to manually type any value in Compose Window header entries or just select from the available choices in the associated dropdown list.'),
    l_gui_warn_send_multi => _('Warn when sending to more than'),
    l_gui_warn_send_multi_units => _('recipients'),
    h_gui_warn_send_multi => _('Show a warning dialogue when sending to more recipients than specified. Use 0 to disable this check.'),
    l_gui_next_del => _('Select next message on delete'),
    h_gui_next_del => _('When deleting a message, toggles between selecting the next one (newer message) or the previous one (older message).'),

    l_beh_hover_t => _('Drag \'n\' drop hover timeout'),
    l_beh_hover_t_units => _('milliseconds'),
    h_beh_hover_t => _('Time in milliseconds that will cause a folder tree to expand when the mouse cursor is held over it during drag and drop.'),
    l_beh_dangerous => _('Don\'t confirm deletions (dangerous!)'),
    h_beh_dangerous => _('Don\'t ask for confirmation before definitive deletion of emails.'),
    l_beh_flowed => _('Respect format=flowed in messages'),
    h_beh_flowed => _('Respect format=flowed on text/plain message parts. This will cause some mails to have long lines, but will fix some URLs that would otherwise be wrapped.'),
    l_beh_parts_rw => _('Allow writable temporary files'),
    h_beh_parts_rw => _('Saves temporary files when opening attachment with write bit set.'),
    l_beh_skip_ssl => _('Don\'t check SSL certificates'),
    h_beh_skip_ssl => _('Disables the verification of SSL certificates.'),
    l_beh_up_step => _('Progress bar update step every'),
    l_beh_up_step_units => _('items'),
    h_beh_up_step => _('Update stepping in progress bars.'),
    l_beh_thread_a => _('Maximum age when threading by subject'),
    l_beh_thread_a_units => _('days'),
    h_beh_thread_a => _('Number of days to include a message in a thread when using "Thread using subject in addition to standard headers".'),
    l_beh_unsafe_ssl => _('Allow unsafe SSL certificates'),
    h_beh_unsafe_ssl => _('Allows Claws Mail to remember multiple SSL certificates for a given server/port.'),
    l_beh_use_utf8 => _('Force UTF-8 for broken mails'),
    h_beh_use_utf8 => _('Use UTF-8 encoding for broken mails instead of current locale.'),
    l_beh_warn_dnd => _('Warn on drag \'n\' drop'),
    h_beh_warn_dnd => _('Display a confirmation dialogue on drag \'n\' drop of folders.'),
    l_beh_out_ascii => _('Outgoing messages fallback to ASCII'),
    h_beh_out_ascii => _('If allowed by content, ASCII will be used to encode outgoing messages, otherwise the user-defined encoding is always enforced.'),
    l_beh_pp_unsel => _('Primary paste unselects selection'),
    h_beh_pp_unsel => _('Controls how pasting using middle-click changes the selected text and insertion point.'),
    l_beh_inline_at => _('Show inline attachments'),
    h_beh_inline_at => _('Allows to hide inline attachments already shown in mail structure view.'),
    l_beh_addr_swc => _('Address search in compose window matches any'),
    h_beh_addr_swc => _('On Tab-key completion, address text will match any part of the string or only from the start.'),
    l_beh_fold_swc => _('Folder search in folder selector matches any'),
    h_beh_fold_swc => _('On folder name completion text will match any part of the string or only from the start.'),
    l_beh_rewrite_ff => _('Rewrite first \'From\' using QP encoding'),
    h_beh_rewrite_ff => _('Workaround some servers which convert first \'From\' to \'>From\' by using Quoted-Printable transfer encoding instead of 7bit/8bit encoding.'),

    l_col_emphasis => _('X-Mailer header'),
    h_col_emphasis => _('The colour used for the X-Mailer line when its value is Claws Mail.'),
    l_col_log_err => _('Error messages'),
    h_col_log_err => _('Colour for error messages in log window.'),
    l_col_log_in => _('Server messages'),
    h_col_log_in => _('Colour for messages received from servers in log window.'),
    l_col_log_msg => _('Standard messages'),
    h_col_log_msg => _('Colour for messages in log window.'),
    l_col_log_out => _('Client messages'),
    h_col_log_out => _('Colour for messages sent to servers in log window.'),
    l_col_log_warn => _('Warning messages'),
    h_col_log_warn => _('Colour for warning messages in log window.'),

    l_col_tags_bg => _('Tags background'),
    h_col_tags_bg => _('Background colour for tags in message view.'),
    l_col_tags_text => _('Tags text'),
    h_col_tags_text => _('Text colour for tags in message view.'),

    l_col_default_header_bg => _('Default headers background'),
    h_col_default_header_bg => _('Background colour for default headers in compose window.'),
    l_col_default_header_text => _('Default headers text'),
    h_col_default_header_text => _('Text colour for default headers in compose window.'),

    l_col_qs_active_bg => _('Active quick search background'),
    h_col_qs_active_bg => _('Background colour for active quick search.'),
    l_col_qs_active_text => _('Active quick search text'),
    h_col_qs_active_text => _('Text colour for active quick search.'),
    l_col_qs_error_bg => _('Quick search error background'),
    h_col_qs_error_bg => _('Background colour for quick search error.'),
    l_col_qs_error_text => _('Quick search error text'),
    h_col_qs_error_text => _('Text colour for quick search error.'),

    l_col_diff_add => _('Added lines'),
    h_col_diff_add => _('Colour for added lines in patches.'),
    l_col_diff_del => _('Deleted lines'),
    h_col_diff_del => _('Colour for deleted lines in patches.'),
    l_col_diff_hunk => _('Hunk lines'),
    h_col_diff_hunk => _('Colour for hunk headers in patches.'),

    l_win_x => _('X position'),
    h_win_x => _('X coordinate for window\'s top-left corner.'),
    l_win_y => _('Y position'),
    h_win_y => _('Y coordinate for window\'s top-left corner.'),
    l_win_w => _('Width'),
    h_win_w => _('Window\'s width in pixels.'),
    l_win_h => _('Height'),
    h_win_h => _('Window\'s height in pixels.'),

    l_win_main_mx => _('Maximized'),
    h_win_main_mx => _('Changes window maximized status.'),
    l_win_main_fs => _('Full-screen'),
    h_win_main_fs => _('Changes full screen status.'),

    l_acc_gtls_set => _('Use custom GnuTLS priority'),
    h_acc_gtls_set => _('Enables using user provided GnuTLS priority string.'),
    l_acc_gtls_pri => _('GnuTLS priority'),
    h_acc_gtls_pri => _('Value to use as GnuTLS priority string if custom priority check is enabled. Otherwise this value is ignored.'),

    l_plu_gpg_alimit => _('Autocompletion limit'),
    h_plu_gpg_alimit => _('Limits the number of addresses obtained from keyring through autocompletion. Use 0 to get all matches.'),
    l_plu_lav_burl => _('Base URL'),
    h_plu_lav_burl => _('This is the URL where avatar requests are sent. You can use the one of your own libravatar server, if available.'),
    l_plu_prl_flvb => _('Log level'),
    h_plu_prl_flvb => _('Verbosity level of log, accumulative.'),
    l_plu_prl_none => _('None'),
    l_plu_prl_manual => _('Manual'),
    l_plu_prl_action => _('Actions'),
    l_plu_prl_match => _('Matches'),
);

# data and metadata of resource files
my $CONFIGDATA;
my $CONFIGMETA;
my $ACCOUNTDATA;
my $ACCOUNTMETA;
# all preferences read by load_preferences
my %PREFS = ();
my %ACPREFS = ();
my %PLPREFS = ();
# values of all preferences handled by clawsker
my %HPVALUE = ();
my %ACHPVALUE = ();
my %PLHPVALUE = ();
# default config dir and file name
my $ALTCONFIGDIR = FALSE;
my $CONFIGDIR = catdir ($ENV{HOME}, '.claws-mail');
my $CONFIGRC = 'clawsrc';
my $ACCOUNTRC = 'accountrc';
# supported and available plugins lists
my @PLUGINS = qw(AttRemover GPG ManageSieve Libravatar PerlPlugin);
my @AVPLUGINS = ();
# loaded hotkeys from load_menurc
my $HOTKEYS;
# current tree selection
my $SELHOTKEY;
# loaded icons
my @APPICONS = ();
# modification flag
my $MODIFIED = 0;

use constant {
    # index constants for preference arrays
    NAME  => 0, # the name on the rc file
    LABEL => 1, # the label on the GUI
    DESC  => 2, # the description for the hint/help
    TYPE  => 3, # data type: bool, int, float, string, color
    CMVER => 4, # lowest[,highest] Claws Mail version(s) the feature exists
    CMDEF => 5, # default value for the preference in Claws Mail
    PLUGIN => 6, # plugin section (only in plugin preferences)
    # constants for GUI spacing
    HBOX_PAD => 5,
    GRID_SPC => 9,
    # for data references indexing
    VALUE => 0,
    IVALUE => 1,
    # hotkey list store columns
    C_LABEL => 0,
    C_HOTKEY => 1,
    C_GROUP => 2,
    C_ACCEL => 3,
    C_ODDITY => 4,
};

# version functions

sub version_greater_or_equal {
    my ($version, $refvers) = @_;
    return TRUE if (length($version) == 0 and length($refvers) >= 0);
    return FALSE if (length($version) >= 0 and length($refvers) == 0);
    return TRUE if (version->parse($version) >= version->parse($refvers));
    return FALSE;
}

sub get_claws_version {
    $_ = which ('claws-mail');
    return "" unless defined $_; # not installed
    my $res = "";
    $_ = qx/$_ -v/;
    chomp;
    my @fver = split (/ /);
    die "Invalid version string" unless ($fver[2] eq "version");
    my @ver = split (/\./, $fver[3]);
    $res .= "$ver[0].";
    $res .= "$ver[1].";
    if ($ver[2] =~ /(\d+)git(\d+)/) {
        $res .= "$1.$2";
    }
    else {
        $res .= "$ver[2].0";
    }
    return $res;
}

# data handlers and auxiliar functions

sub handle_bool_value {
    my ($widget, $event, $dataref) = @_;
    $$dataref->[VALUE] = ($widget->get_active ())? '1': '0';
    $MODIFIED += $$dataref->[VALUE] != $$dataref->[IVALUE]? 1: -1
        if defined $$dataref->[IVALUE];
}

sub handle_int_value {
    my ($widget, $event, $dataref) = @_;
    $_ = $widget->get_text ();
    s/^\s+//;
    s/\s+$//;
    if (/^[0-9]+$/) {
        $$dataref->[VALUE] = $_;
        $widget->set_text ($_);
    }
    else {
        $widget->set_text ($$dataref->[VALUE]);
    }
    $MODIFIED += $$dataref->[VALUE] != $$dataref->[IVALUE]? 1: -1
        if defined $$dataref->[IVALUE];
}

sub handle_nchar_value {
    my ($widget, $event, $dataref, $minlen, $maxlen) = @_;
    $_ = substr ($widget->get_text (), 0, $maxlen);
    $widget->set_text ($_);
    $$dataref->[VALUE] = $_;
    $MODIFIED += $$dataref->[VALUE] ne $$dataref->[IVALUE]? 1: -1
        if defined $$dataref->[IVALUE];
}

sub gdk_rgba_from_str {
    my ($str) = @_;
    my ($rr, $gg, $bb) = (0, 0 ,0);
    $_ = uc ($str);
    if (/\#([A-F0-9][A-F0-9])([A-F0-9][A-F0-9])([A-F0-9][A-F0-9])/) {
        $rr = hex($1) / 256;
        $gg = hex($2) / 256;
        $bb = hex($3) / 256;
    }
    my $color = Gtk3::Gdk::RGBA->new ($rr, $gg, $bb, 1.0);
    return $color;
}

sub str_from_gdk_rgba {
    my ($color) = @_;
    my $rr = $color->red * 256;
    my $gg = $color->green * 256;
    my $bb = $color->blue * 256;
    my $str = sprintf ("#%.2x%.2x%.2x", $rr, $gg, $bb);
    return $str;
}

sub handle_color_value {
    my ($widget, $event, $dataref) = @_;
    my $newcol = $widget->get_rgba;
    $$dataref->[VALUE] = str_from_gdk_rgba ($newcol);
    $MODIFIED += $$dataref->[VALUE] ne $$dataref->[IVALUE]? 1: -1
        if defined $$dataref->[IVALUE];
}

sub handle_selection_value {
    my ($widget, $event, $dataref) = @_;
    $$dataref->[VALUE] = $widget->get_active;
    $MODIFIED += $$dataref->[VALUE] ne $$dataref->[IVALUE]? 1: -1
        if defined $$dataref->[IVALUE];
}

sub get_rc_filename {
    return catfile ($CONFIGDIR, $CONFIGRC);
}

sub get_ac_rc_filename {
    return catfile ($CONFIGDIR, $ACCOUNTRC);
}

sub get_menurc_filename {
    return catfile ($CONFIGDIR, "menurc");
}

sub set_rc_filename {
    my ($fullname) = @_;
    my @parts = splitpath ($fullname);
    $CONFIGRC = $parts[$#parts];
    $parts[$#parts] = '';
    $CONFIGDIR = catpath (@parts);
}

sub log_message {
    my ($mesg, $fatal) = @_;
    if (defined($fatal) && $fatal eq 'die') {
        die "$NAME: $mesg\n";
    }
    if ($VERBOSE) {
        print "$NAME: $mesg\n";
    }
}

sub message_dialog {
    my ($parent, $title, $markup, $type, $buttons) = @_;
    my $flags = [qw/modal destroy-with-parent/];
    my $dialog = Gtk3::Dialog->new_with_buttons (
        $title, $parent, $flags, @$buttons
    );
    my $label = Gtk3::Label->new;
    $label->set_markup ($markup);
    my $icon = undef;
    if ($type eq 'error') {
      $icon = Gtk3::Image->new_from_icon_name('dialog-error', 'GTK_ICON_SIZE_DIALOG');
    } elsif ($type eq 'warning') {
      $icon = Gtk3::Image->new_from_icon_name('dialog-warning', 'GTK_ICON_SIZE_DIALOG');
    } elsif ($type eq 'question') {
      $icon = Gtk3::Image->new_from_icon_name('dialog-question', 'GTK_ICON_SIZE_DIALOG');
    }
    my $hbox = Gtk3::Box->new ('horizontal', 5);
    $hbox->pack_start ($icon, FALSE, FALSE, 5) if defined $icon;
    $hbox->pack_start ($label, FALSE, FALSE, 5);
    my $dialogbox = $dialog->get_content_area;
    $dialogbox->add ($hbox);
    $dialogbox->show_all;
    return $dialog;
}

sub error_dialog {
    my ($emsg) = @_;
    my $markup = "<span weight=\"bold\" size=\"large\">" . $emsg . "</span>";
    my $errordlg = message_dialog (
        $main_window, _('Clawsker error'), $markup, 'error', [ 'gtk-cancel', 0 ]
    );
    $errordlg->run;
    $errordlg->destroy;
}

sub claws_is_running {
    my $emsg = _('Error: seems Claws Mail is currently running, close it first.');
    log_message ($emsg);
    error_dialog ($emsg);
    return FALSE;
}

sub check_claws_not_running {
    return TRUE if $READONLY;
    my $tmpdir = File::Spec->tmpdir ();
    my $lockdir = catfile ($tmpdir, "claws-mail-$<");
    -d $lockdir and do {
        $_ = $CONFIGDIR;
        s/\/$//;
        my $socket = catfile ($lockdir, md5_hex ($_));
        -S $socket and return claws_is_running ();
    };
    return TRUE;
}

sub check_rc_file {
    my ($rcfile) = @_;
    (defined($rcfile) && -f $rcfile) or do {
        my $emsg = _('Error: resource file for Claws Mail was not found.');
        log_message ($emsg);
        error_dialog ($emsg);
        return FALSE;
    };
    return TRUE;
}

sub set_widget_hint {
    my ($wdgt, $hint) = @_;
    $wdgt->set_tooltip_text ($hint);
    $wdgt->set_has_tooltip (TRUE);
}

sub set_widget_sens {
    my ($wdgt, $versions) = @_;
    my @ver = split(/,/, $versions);
    if ($#ver == 1) {
      $wdgt->set_sensitive (
        version_greater_or_equal ($CLAWSV, $ver[0])
        and version_greater_or_equal ($ver[1], $CLAWSV)
      );
    } else {
        $wdgt->set_sensitive (version_greater_or_equal ($CLAWSV, $ver[0]));
    }
}

# graphic element creation

sub new_hbox_spaced_pack {
    my $hbox = Gtk3::HBox->new (FALSE);
    foreach (@_) {
        $hbox->pack_start ($_, FALSE, FALSE, HBOX_PAD);
    }
    return $hbox;
}

sub new_check_button_for($$$) {
    my ($hash, $key, $vhash) = @_;
    my $name = $$hash{$key}[NAME];
    my $label = $$hash{$key}[LABEL];
    #
    my $cb = Gtk3::CheckButton->new ($label);
    my $value = $$vhash{$name}[VALUE];
    $value //= $$hash{$key}[CMDEF];
    $cb->set_active ($value eq '1');
    $cb->signal_connect (clicked => sub {
            my ($w, $e) = @_;
            handle_bool_value ($w, $e, \$$vhash{$name});
        });
    set_widget_hint ($cb, $$hash{$key}[DESC]);
    set_widget_sens ($cb, $$hash{$key}[CMVER]);
    #
    return new_hbox_spaced_pack ($cb);
}

sub new_text_box_for_int($$$) {
    my ($hash, $key, $vhash) = @_;
    my $name = $$hash{$key}[NAME];
    my $label = $$hash{$key}[LABEL];
    my @type = split (/,/, $$hash{$key}[TYPE]);
    push (@type, 0), push (@type, 10000) unless ($#type > 0);
    #
    my $gunits = undef;
    if (ref $label eq 'ARRAY') {
        $gunits = Gtk3::Label->new ($label->[1]);
        $label = $label->[0];
    }
    my $glabel = Gtk3::Label->new ($label);
    my $pagei = int (($type[2] - $type[1]) / 10);
    my $gentry = Gtk3::SpinButton->new_with_range ($type[1], $type[2], $pagei);
    my $value = $$vhash{$name}[VALUE];
    $value //= $$hash{$key}[CMDEF];
    $gentry->set_numeric (TRUE);
    $gentry->set_value ($value);
    $gentry->signal_connect('value-changed' => sub {
            my ($w, $e) = @_;
            handle_int_value ($w, $e, \$$vhash{$name});
        });
    set_widget_hint ($gentry, $$hash{$key}[DESC]);
    set_widget_sens ($gentry, $$hash{$key}[CMVER]);
    $glabel->set_sensitive ($gentry->get_sensitive);
    $gunits->set_sensitive ($gentry->get_sensitive) if ($gunits);
    #
    return new_hbox_spaced_pack ($glabel, $gentry, $gunits) if ($gunits);
    return new_hbox_spaced_pack ($glabel, $gentry);
}

sub new_text_box_for_nchar($$$) {
    my ($hash, $key, $vhash) = @_;
    my $name = $$hash{$key}[NAME];
    my $label = $$hash{$key}[LABEL];
    my @type = split (/,/, $$hash{$key}[TYPE]); # char,minlen,maxlen,width
    my $glabel = Gtk3::Label->new ($label);
    my $gentry = Gtk3::Entry->new ();
    $gentry->set_max_length($type[2]) if defined ($type[2]);
    my $width = $type[3];
    $width //= $type[2];
    $gentry->set_width_chars(int ($width) + 2) if defined ($width);
    my $value = $$vhash{$name}[VALUE];
    $value //= $$hash{$key}[CMDEF];
    $gentry->set_text ($value);
    $gentry->signal_connect('key-release-event' => sub {
            my ($w, $e) = @_;
            handle_nchar_value ($w, $e, \$$vhash{$name}, $type[1], $type[2]);
        });
    set_widget_hint ($gentry, $$hash{$key}[DESC]);
    set_widget_sens ($gentry, $$hash{$key}[CMVER]);
    $glabel->set_sensitive ($gentry->get_sensitive);
    #
    return new_hbox_spaced_pack ($glabel, $gentry);
}

sub new_color_button_for($$$) {
    my ($hash, $key, $vhash) = @_;
    my $name = $$hash{$key}[NAME];
    my $label = $$hash{$key}[LABEL];
    #
    my $value = $$vhash{$name}[VALUE];
    $value //= $$hash{$key}[CMDEF];
    my $col = gdk_rgba_from_str ($value);
    my $glabel = Gtk3::Label->new ($label);
    my $button = Gtk3::ColorButton->new_with_rgba ($col);
    $button->set_title ($label);
    $button->set_relief ('none');
    $button->signal_connect ('color-set' => sub {
            my ($w, $e) = @_;
            handle_color_value ($w, $e, \$$vhash{$name});
        });
    set_widget_hint ($button, $$hash{$key}[DESC]);
    set_widget_sens ($button, $$hash{$key}[CMVER]);
    $glabel->set_sensitive ($button->get_sensitive);
    #
    return new_hbox_spaced_pack ($button, $glabel);
}

sub new_selection_box_for($$$) {
    my ($hash, $key, $vhash) = @_;
    my $name = $$hash{$key}[NAME];
    my $label = $$hash{$key}[LABEL];
    #
    my $glabel = Gtk3::Label->new ($label);
    my $combo = Gtk3::ComboBoxText->new;
    my @options = split (';', $$hash{$key}[TYPE]);
    foreach my $opt (@options) {
        my ($index, $textkey) = split ('=', $opt);
        $combo->insert (-1, $index, $xl::s{$textkey});
    }
    $combo->signal_connect ('changed' => sub {
            my ($w, $e) = @_;
            handle_selection_value ($w, $e, \$$vhash{$name});
        });
    my $value = $$vhash{$name}[VALUE];
    $value //= $$hash{$key}[CMDEF];
    $combo->set_active ($value);
    set_widget_hint ($combo, $$hash{$key}[DESC]);
    set_widget_sens ($combo, $$hash{$key}[CMVER]);
    $glabel->set_sensitive ($combo->get_sensitive);
    #
    return new_hbox_spaced_pack ($glabel, $combo);
}

# more graphic helpers

sub new_grid {
    my ($border_w, $row_s, $col_s) = @_;
    $border_w //= GRID_SPC;
    $row_s //= GRID_SPC;
    $col_s //= GRID_SPC;
    my $grid = Gtk3::Grid->new;
    $grid->set_border_width ($border_w);
    $grid->set_row_spacing ($row_s);
    $grid->set_column_spacing ($col_s);
    return $grid;
}

sub new_label {
    my $text = shift;
    $text //= '';
    my $label = Gtk3::Label->new ($text);
    $label->set_alignment (0, 0.5);
    return $label;
}

sub new_title {
    my $text = shift;
    $text //= '';
    my $label = Gtk3::Label->new ('<b>' . $text . '</b>');
    $label->set_use_markup (TRUE);
    $label->set_alignment (0, 0.5);
    return $label;
}

sub new_grid_pack {
    my ($width, $height, $widget) = @_;
    my $grid = new_grid ();
    $grid->set_column_homogeneous (TRUE);
    for (my $i = 0; $i < $width; ++$i) {
        for (my $j = 0; $j < $height; ++$j) {
            my $wid = $widget->[$j]->[$i];
            next unless defined $wid;
            my $ww = (($i + 1 < $width) and (defined $widget->[$j]->[$i + 1]))
                ? 1
                : $width - $i;
            if (ref $wid) {
                $grid->attach ($wid, $i, $j, $ww, 1);
            } else { # not a widget
                if ('--' eq $wid) { # a separator
                    $grid->attach (Gtk3::Separator->new ('horizontal'),
                        $i, $j, $ww, 1);
                } else { # or a title
                    $grid->attach (new_title ($wid),
                        $i, $j, $ww, 1);
                }
            }
        }
    }
    return $grid;
}

# preference maps and corresponding page creation subs

%pr::oth = ( # other preferences
    use_dlg => [
        'addressbook_use_editaddress_dialog',
        $xl::s{l_oth_use_dlg},
        $xl::s{h_oth_use_dlg},
        'bool',
        '2.7.0',
        '0',
    ],
    max_use => [
        'cache_max_mem_usage',
        [ $xl::s{l_oth_max_use}, $xl::s{l_oth_max_use_units} ],
        $xl::s{h_oth_max_use},
        'int,0,524288', # 0 Kb - 512 Mb
        '0.7.8.36',
        '4096',
    ],
    min_time => [
        'cache_min_keep_time',
        [ $xl::s{l_oth_min_time}, $xl::s{l_oth_min_time_units} ],
        $xl::s{h_oth_min_time},
        'int,0,120', # 0 minutes - 2 hours
        '0.7.8.36',
        '15',
    ],
    use_netm => [
        'use_networkmanager',
        $xl::s{l_oth_use_netm},
        $xl::s{h_oth_use_netm},
        'bool',
        '3.3.1',
        '1',
    ],
    mp_rounds => [
        'master_passphrase_pbkdf2_rounds',
        $xl::s{l_oth_mp_rounds},
        $xl::s{h_oth_mp_rounds},
        'int,50000,1000000',
        '3.13.2.110',
        '50000',
    ],
);

sub new_other_page() {
    return new_grid_pack (1, 12, [
        [ _('Addressbook') ],
        [ new_check_button_for(\%pr::oth, 'use_dlg', \%HPVALUE) ],
        [ '--' ],
        [ _('Memory') ],
        [ new_text_box_for_int(\%pr::oth, 'max_use', \%HPVALUE) ],
        [ new_text_box_for_int(\%pr::oth, 'min_time', \%HPVALUE) ],
        [ '--' ],
        [ _('NetworkManager') ],
        [ new_check_button_for(\%pr::oth, 'use_netm', \%HPVALUE) ],
        [ '--' ],
        [ _('Master passphrase') ],
        [ new_text_box_for_int(\%pr::oth, 'mp_rounds', \%HPVALUE) ]
    ]);
}

%pr::gui = ( # gui bells and whistles
    b_unread => [
        'bold_unread',
        $xl::s{l_gui_b_unread},
        $xl::s{h_gui_b_unread},
        'bool',
        '0.5.3',
        '1',
    ],
    no_markup => [
        'compose_no_markup',
        $xl::s{l_gui_no_markup},
        $xl::s{h_gui_no_markup},
        'bool',
        '2.1.0.16',
        '0',
    ],
    dot_lines => [
        'enable_dotted_lines',
        $xl::s{l_gui_dot_lines},
        $xl::s{h_gui_dot_lines},
        'bool',
        '2.4.0.115,3.7.10.44',
        '0',
    ],
    h_scroll => [
        'enable_hscrollbar',
        $xl::s{l_gui_h_scroll},
        $xl::s{h_gui_h_scroll},
        'bool',
        '0.8.6.18',
        '1',
    ],
    swp_from => [
        'enable_swap_from',
        $xl::s{l_gui_swp_from},
        $xl::s{h_gui_swp_from},
        'bool',
        '1.9.13.40',
        '0',
    ],
    v_scroll => [
        'folderview_vscrollbar_policy',
        $xl::s{l_gui_v_scroll},
        $xl::s{h_gui_v_scroll},
        '0=l_gui_v_scroll_show;1=l_gui_v_scroll_auto;2=l_gui_v_scroll_hide',
        '0.7.8.14',
        '0',
    ],
    from_show => [
        'summary_from_show',
        $xl::s{l_gui_from_show},
        $xl::s{h_gui_from_show},
        '0=l_gui_from_show_name;1=l_gui_from_show_addr;2=l_gui_from_show_both',
        '3.7.10',
        '0',
    ],
    strip_off => [
        'stripes_color_offset',
        $xl::s{l_gui_strip_off},
        $xl::s{h_gui_strip_off},
        'int,0,40000', # no idea what this number means
        '2.4.0.186',
        '4000',
    ],
    cursor_v => [
        'textview_cursor_visible',
        $xl::s{l_gui_cursor_v},
        $xl::s{h_gui_cursor_v},
        'bool',
        '0.0.0',
        '0',
    ],
    toolbar_d => [
        'toolbar_detachable',
        $xl::s{l_gui_toolbar_d},
        $xl::s{h_gui_toolbar_d},
        'bool',
        '0.0.0',
        '0',
    ],
    strip_all => [
        'use_stripes_everywhere',
        $xl::s{l_gui_strip_all},
        $xl::s{h_gui_strip_all},
        'bool',
        '0.0.0',
        '1',
    ],
    strip_sum => [
        'use_stripes_in_summaries',
        $xl::s{l_gui_strip_sum},
        $xl::s{h_gui_strip_sum},
        'bool',
        '0.0.0',
        '1',
    ],
    two_linev => [
        'two_line_vertical',
        $xl::s{l_gui_two_line_v},
        $xl::s{h_gui_two_line_v},
        'bool',
        '3.4.0.7',
        '0',
    ],
    margin_co => [
        'show_compose_margin',
        $xl::s{l_gui_margin_co},
        $xl::s{h_gui_margin_co},
        'bool',
        '3.7.6.7',
        '0',
    ],
    mview_date => [
        'msgview_date_format',
        $xl::s{l_gui_mview_date},
        $xl::s{h_gui_mview_date},
        'bool',
        '3.7.8.42',
        '0',
    ],
    zero_char => [
        'zero_replacement_char',
        $xl::s{l_gui_zero_char},
        $xl::s{h_gui_zero_char},
        'char,1,1',
        '2.8.1.77',
        '0',
    ],
    type_any => [
        'type_any_header',
        $xl::s{l_gui_type_any},
        $xl::s{h_gui_type_any},
        'bool',
        '3.12.0.44',
        '0',
    ],
    warn_send_multi => [
        'warn_sending_many_recipients_num',
        [ $xl::s{l_gui_warn_send_multi}, $xl::s{l_gui_warn_send_multi_units} ],
        $xl::s{h_gui_warn_send_multi},
        'int,0,1000',
        '3.14.1.125',
        '3.15.0.28',
    ],
    next_del => [
        'next_on_delete',
        $xl::s{l_gui_next_del},
        $xl::s{h_gui_next_del},
        'bool',
        '3.13.0.5',
        '0',
    ],
);

sub new_gui_page() {
    return new_grid_pack (2, 24, [
        [ _('Coloured stripes') ],
        [ new_check_button_for (\%pr::gui, 'strip_all', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'strip_sum', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::gui, 'strip_off', \%HPVALUE) ],
        [ '--' ],
        [ _('Message List') ],
        [ new_check_button_for (\%pr::gui, 'b_unread', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'swp_from', \%HPVALUE) ],
        [ new_check_button_for (\%pr::gui, 'two_linev', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'next_del', \%HPVALUE) ],
        [ new_selection_box_for (\%pr::gui, 'from_show', \%HPVALUE) ],
        [ '--' ],
        [ _('Message View') ],
        [ new_check_button_for (\%pr::gui, 'cursor_v', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'mview_date', \%HPVALUE) ],
        [ '--' ],
        [ _('Compose window') ],
        [ new_check_button_for (\%pr::gui, 'no_markup', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'margin_co', \%HPVALUE) ],
        [ new_check_button_for (\%pr::gui, 'type_any', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::gui, 'warn_send_multi', \%HPVALUE) ],
        [ '--' ],
        [ _('Scroll bars') ],
        [ new_check_button_for (\%pr::gui, 'h_scroll', \%HPVALUE),
            new_selection_box_for (\%pr::gui, 'v_scroll', \%HPVALUE) ],
        [ '--' ],
        [ _('Other') ],
        [ new_check_button_for (\%pr::gui, 'dot_lines', \%HPVALUE),
            new_check_button_for (\%pr::gui, 'toolbar_d', \%HPVALUE) ],
        [ new_text_box_for_nchar (\%pr::gui, 'zero_char', \%HPVALUE) ]
    ]);
}

%pr::beh = ( # tweak some behaviour
    hover_t => [
        'hover_timeout',
        [ $xl::s{l_beh_hover_t}, $xl::s{l_beh_hover_t_units} ],
        $xl::s{h_beh_hover_t},
        'int,100,3000', # 0.1 seconds - 3 seconds
        '0.0.0',
        '500',
    ],
    dangerous => [
        'live_dangerously',
        $xl::s{l_beh_dangerous},
        $xl::s{h_beh_dangerous},
        'bool',
        '0.0.0',
        '0',
    ],
    flowed => [
        'respect_flowed_format',
        $xl::s{l_beh_flowed},
        $xl::s{h_beh_flowed},
        'bool',
        '0.0.0',
        '0',
    ],
    parts_rw => [
        'save_parts_readwrite',
        $xl::s{l_beh_parts_rw},
        $xl::s{h_beh_parts_rw},
        'bool',
        '0.0.0',
        '0',
    ],
    skip_ssl => [
        'skip_ssl_cert_check',
        $xl::s{l_beh_skip_ssl},
        $xl::s{h_beh_skip_ssl},
        'bool',
        '0.0.0',
        '0',
    ],
    up_step => [
        'statusbar_update_step',
        [ $xl::s{l_beh_up_step}, $xl::s{l_beh_up_step_units} ],
        $xl::s{h_beh_up_step},
        'int,1,200', # 1 item - 200 items
        '0.0.0',
        '10',
    ],
    thread_a => [
        'thread_by_subject_max_age',
        [ $xl::s{l_beh_thread_a}, $xl::s{l_beh_thread_a_units} ],
        $xl::s{h_beh_thread_a},
        'int,1,30', # 1 day - 30 days
        '0.0.0',
        '10',
    ],
    unsafe_ssl => [
        'unsafe_ssl_certs',
        $xl::s{l_beh_unsafe_ssl},
        $xl::s{h_beh_unsafe_ssl},
        'bool',
        '0.0.0',
        '0',
    ],
    use_utf8 => [
        'utf8_instead_of_locale_for_broken_mail',
        $xl::s{l_beh_use_utf8},
        $xl::s{h_beh_use_utf8},
        'bool',
        '1.9.14.49',
        '0',
    ],
    warn_dnd => [
        'warn_dnd',
        $xl::s{l_beh_warn_dnd},
        $xl::s{h_beh_warn_dnd},
        'bool',
        '0.0.0',
        '1',
    ],
    out_ascii => [
        'outgoing_fallback_to_ascii',
        $xl::s{l_beh_out_ascii},
        $xl::s{h_beh_out_ascii},
        'bool',
        '3.4.0.37',
        '1',
    ],
    pp_unsel => [
        'primary_paste_unselects',
        $xl::s{l_beh_pp_unsel},
        $xl::s{h_beh_pp_unsel},
        'bool',
        '3.6.1.35',
        '0',
    ],
    inline_at => [
        'show_inline_attachments',
        $xl::s{l_beh_inline_at},
        $xl::s{h_beh_inline_at},
        'bool',
        '3.7.8.48',
        '1',
    ],
    addr_swc => [
        'address_search_wildcard',
        $xl::s{l_beh_addr_swc},
        $xl::s{h_beh_addr_swc},
        'bool',
        '3.9.3.18',
        '0',
    ],
    fold_swc => [
        'folder_search_wildcard',
        $xl::s{l_beh_fold_swc},
        $xl::s{h_beh_fold_swc},
        'bool',
        '3.9.3.18',
        '0',
    ],
    rewrite_ff => [
        'rewrite_first_from',
        $xl::s{l_beh_rewrite_ff},
        $xl::s{h_beh_rewrite_ff},
        'bool',
        '3.14.0.94',
        '0',
    ],
);

sub new_behaviour_page() {
    return new_grid_pack (2, 20, [
        [ _('Drag \'n\' drop') ],
        [ new_text_box_for_int (\%pr::beh, 'hover_t', \%HPVALUE) ],
        [ new_check_button_for (\%pr::beh, 'warn_dnd', \%HPVALUE) ],
        [ '--' ],
        [ _('Secure Sockets Layer') ],
        [ new_check_button_for (\%pr::beh, 'skip_ssl', \%HPVALUE),
            new_check_button_for (\%pr::beh, 'unsafe_ssl', \%HPVALUE) ],
        [ '--' ],
        [ _('Messages') ],
        [ new_check_button_for (\%pr::beh, 'flowed', \%HPVALUE),
            new_check_button_for (\%pr::beh, 'out_ascii', \%HPVALUE) ],
        [ new_check_button_for (\%pr::beh, 'parts_rw', \%HPVALUE),
            new_check_button_for (\%pr::beh, 'pp_unsel', \%HPVALUE) ],
        [ new_check_button_for (\%pr::beh, 'use_utf8', \%HPVALUE),
            new_check_button_for (\%pr::beh, 'inline_at', \%HPVALUE) ],
        [ new_check_button_for (\%pr::beh, 'dangerous', \%HPVALUE),
            new_check_button_for (\%pr::beh, 'rewrite_ff', \%HPVALUE) ],
        [ '--' ],
        [ _('Completion') ],
        [ new_check_button_for (\%pr::beh, 'addr_swc', \%HPVALUE) ],
        [ new_check_button_for (\%pr::beh, 'fold_swc', \%HPVALUE) ],
        [ '--' ],
        [ _('Other') ],
        [ new_text_box_for_int (\%pr::beh, 'up_step', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::beh, 'thread_a', \%HPVALUE) ]
    ]);
}

%pr::col = ( # a variety of colours
    emphasis => [
        'emphasis_color',
        $xl::s{l_col_emphasis},
        $xl::s{h_col_emphasis},
        'color',
        '0.0.0',
        '#0000cf',
    ],
    log_err => [
        'log_error_color',
        $xl::s{l_col_log_err},
        $xl::s{h_col_log_err},
        'color',
        '0.0.0',
        '#af0000',
    ],
    log_in => [
        'log_in_color',
        $xl::s{l_col_log_in},
        $xl::s{h_col_log_in},
        'color',
        '0.0.0',
        '#000000',
    ],
    log_msg => [
        'log_msg_color',
        $xl::s{l_col_log_msg},
        $xl::s{h_col_log_msg},
        'color',
        '0.0.0',
        '#00af00',
    ],
    log_out => [
        'log_out_color',
        $xl::s{l_col_log_out},
        $xl::s{h_col_log_out},
        'color',
        '0.0.0',
        '#0000ef',
    ],
    log_warn => [
        'log_warn_color',
        $xl::s{l_col_log_warn},
        $xl::s{h_col_log_warn},
        'color',
        '0.0.0',
        '#af0000',
    ],
    diff_add => [
        'diff_added_color',
        $xl::s{l_col_diff_add},
        $xl::s{h_col_diff_add},
        'color',
        '3.8.0.54',
        '#008b8b',
    ],
    diff_del => [
        'diff_deleted_color',
        $xl::s{l_col_diff_del},
        $xl::s{h_col_diff_del},
        'color',
        '3.8.0.54',
        '#6a5acd',
    ],
    diff_hunk => [
        'diff_hunk_color',
        $xl::s{l_col_diff_hunk},
        $xl::s{h_col_diff_hunk},
        'color',
        '3.8.0.54',
        '#a52a2a',
    ],
    tags_bg => [
        'tags_bgcolor',
        $xl::s{l_col_tags_bg},
        $xl::s{h_col_tags_bg},
        'color',
        '3.14.1.31',
        '#f5f6be',
    ],
    tags_text => [
        'tags_color',
        $xl::s{l_col_tags_text},
        $xl::s{h_col_tags_text},
        'color',
        '3.14.1.31',
        '#000000',
    ],
    default_header_bg => [
        'default_header_bgcolor',
        $xl::s{l_col_default_header_bg},
        $xl::s{h_col_default_header_bg},
        'color',
        '3.14.1.31',
        '#f5f6be',
    ],
    default_header_text => [
        'default_header_color',
        $xl::s{l_col_default_header_text},
        $xl::s{h_col_default_header_text},
        'color',
        '3.14.1.31',
        '#000000',
    ],
    qs_active_bg => [
        'qs_active_bgcolor',
        $xl::s{l_col_qs_active_bg},
        $xl::s{h_col_qs_active_bg},
        'color',
        '3.14.1.31',
        '#f5f6be',
    ],
    qs_active_text => [
        'qs_active_color',
        $xl::s{l_col_qs_active_text},
        $xl::s{h_col_qs_active_text},
        'color',
        '3.14.1.31',
        '#000000',
    ],
    qs_error_bg => [
        'qs_error_bgcolor',
        $xl::s{l_col_qs_error_bg},
        $xl::s{h_col_qs_error_bg},
        'color',
        '3.14.1.31',
        '#ff7070',
    ],
    qs_error_text => [
        'qs_error_color',
        $xl::s{l_col_qs_error_text},
        $xl::s{h_col_qs_error_text},
        'color',
        '3.14.1.31',
        '#000000',
    ],
);

sub new_colours_page() {
    return new_grid_pack (3, 18, [
        [ _('Message View') ],
        [ new_color_button_for (\%pr::col, 'emphasis', \%HPVALUE) ],
        [ new_color_button_for (\%pr::col, 'tags_text', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'tags_bg', \%HPVALUE) ],
        [ '--' ],
        [ _('Log window') ],
        [ new_color_button_for (\%pr::col, 'log_err', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'log_in', \%HPVALUE) ],
        [ new_color_button_for (\%pr::col, 'log_warn', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'log_out', \%HPVALUE) ],
        [ new_color_button_for (\%pr::col, 'log_msg', \%HPVALUE) ],
        [ '--' ],
        [ _('Viewing patches') ],
        [ new_color_button_for (\%pr::col, 'diff_add', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'diff_del', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'diff_hunk', \%HPVALUE) ],
        [ '--' ],
        [ _('Compose window') ],
        [ new_color_button_for (\%pr::col, 'default_header_text', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'default_header_bg', \%HPVALUE) ],
        [ '--' ],
        [ _('Quick search') ],
        [ new_color_button_for (\%pr::col, 'qs_active_text', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'qs_active_bg', \%HPVALUE) ],
        [ new_color_button_for (\%pr::col, 'qs_error_text', \%HPVALUE) ,
            new_color_button_for (\%pr::col, 'qs_error_bg', \%HPVALUE) ]
    ]);
}

%pr::win = ( # tweak window positions and/or sizes
    main_x => [
        'mainwin_x',
        $xl::s{l_win_x},
        $xl::s{h_win_x},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '16',
    ],
    main_y => [
        'mainwin_y',
        $xl::s{l_win_y},
        $xl::s{h_win_y},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '16',
    ],
    main_w => [
        'mainwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '779',
    ],
    main_h => [
        'mainwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '568',
    ],
    main_mx => [
        'mainwin_maximised',
        $xl::s{l_win_main_mx},
        $xl::s{h_win_main_mx},
        'bool',
        '0.0.0',
        '0',
    ],
    main_fs => [
        'mainwin_fullscreen',
        $xl::s{l_win_main_fs},
        $xl::s{h_win_main_fs},
        'bool',
        '0.0.0',
        '0',
    ],
    msgs_x => [
        'main_messagewin_x',
        $xl::s{l_win_x},
        $xl::s{h_win_x},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '256',
    ],
    msgs_y => [
        'main_messagewin_y',
        $xl::s{l_win_y},
        $xl::s{h_win_y},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '210',
    ],
    msgs_w => [
        'messagewin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '600',
    ],
    msgs_h => [
        'messagewin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '540',
    ],
    send_w => [
        'sendwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '460',
    ],
    send_h => [
        'sendwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    recv_w => [
        'receivewin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '460',
    ],
    recv_h => [
        'receivewin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    fold_x => [
        'folderwin_x',
        $xl::s{l_win_x},
        $xl::s{h_win_x},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '16',
    ],
    fold_y => [
        'folderwin_y',
        $xl::s{l_win_y},
        $xl::s{h_win_y},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '16',
    ],
    fold_w => [
        'folderitemwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '500',
    ],
    fold_h => [
        'folderitemwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    fsel_w => [
        'folderselwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '300',
    ],
    fsel_h => [
        'folderselwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    sour_w => [
        'sourcewin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '600',
    ],
    sour_h => [
        'sourcewin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '500',
    ],
    addr_w => [
        'addressbookwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '520',
    ],
    addr_h => [
        'addressbookwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    adep_w => [
        'addressbookeditpersonwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '640',
    ],
    adep_h => [
        'addressbookeditpersonwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '320',
    ],
    adeg_w => [
        'addressbookeditgroupwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '580',
    ],
    adeg_h => [
        'addressbookeditgroupwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '340',
    ],
    adda_w => [
        'addressaddwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '300',
    ],
    adda_h => [
        'addressaddwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    addf_w => [
        'addressbook_folderselwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '300',
    ],
    addf_h => [
        'addressbook_folderselwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    acce_w => [
        'editaccountwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '500',
    ],
    acce_h => [
        'editaccountwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    acco_w => [
        'accountswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '500',
    ],
    acco_h => [
        'accountswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    filt_w => [
        'filteringwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '500',
    ],
    filt_h => [
        'filteringwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    fila_w => [
        'filteringactionwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '490',
    ],
    fila_h => [
        'filteringactionwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    fild_w => [
        'filtering_debugwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '600',
    ],
    fild_h => [
        'filtering_debugwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    matc_w => [
        'matcherwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '520',
    ],
    matc_h => [
        'matcherwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    pref_w => [
        'prefswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '600',
    ],
    pref_h => [
        'prefswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    temp_w => [
        'templateswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '480',
    ],
    temp_h => [
        'templateswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    acti_w => [
        'actionswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '486',
    ],
    acti_h => [
        'actionswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    acio_w => [
        'actionsiodialog_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '582',
    ],
    acio_h => [
        'actionsiodialog_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '310',
    ],
    tags_w => [
        'tagswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '486',
    ],
    tags_h => [
        'tagswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    sslman_w => [
        'sslmanwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '486',
    ],
    sslman_h => [
        'sslmanwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    plug_w => [
        'pluginswin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    plug_h => [
        'pluginswin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    logw_w => [
        'logwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '520',
    ],
    logw_h => [
        'logwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
    prin_w => [
        'print_previewwin_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '600',
    ],
    prin_h => [
        'print_previewwin_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '0.0.0',
        '-1',
    ],
);

sub new_winpos_subpage_main() {
    return new_grid_pack (3, 7, [
        [ _('Main window'), undef ],
        [ new_text_box_for_int (\%pr::win, 'main_x', \%HPVALUE), undef ],
        [ new_text_box_for_int (\%pr::win, 'main_y', \%HPVALUE), undef ],
        [ new_text_box_for_int (\%pr::win, 'main_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'main_h', \%HPVALUE) ],
        [ new_check_button_for (\%pr::win, 'main_fs', \%HPVALUE), undef ],
        [ new_check_button_for (\%pr::win, 'main_mx', \%HPVALUE), undef ]
    ]);
}

sub new_winpos_subpage_msgs() {
    return new_grid_pack (3, 4, [
        [ _('Message window') ],
        [ new_text_box_for_int (\%pr::win, 'msgs_x', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::win, 'msgs_y', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::win, 'msgs_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'msgs_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_sendrecv() {
    return new_grid_pack (3, 5, [
        [ _('Send window') ],
        [ new_text_box_for_int (\%pr::win, 'send_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'send_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Receive window') ],
        [ new_text_box_for_int (\%pr::win, 'recv_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'recv_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_fold() {
    return new_grid_pack (3, 7, [
        [ _('Folder window') ],
        [ new_text_box_for_int (\%pr::win, 'fold_x', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::win, 'fold_y', \%HPVALUE) ],
        [ new_text_box_for_int (\%pr::win, 'fold_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'fold_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Folder selection window') ],
        [ new_text_box_for_int (\%pr::win, 'fsel_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'fsel_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_addrbook() {
    return new_grid_pack (3, 14, [
        [ _('Addressbook main window') ],
        [ new_text_box_for_int (\%pr::win, 'addr_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'addr_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Edit person window') ],
        [ new_text_box_for_int (\%pr::win, 'adep_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'adep_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Edit group window') ],
        [ new_text_box_for_int (\%pr::win, 'adeg_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'adeg_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Add address window') ],
        [ new_text_box_for_int (\%pr::win, 'adda_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'adda_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Folder select window') ],
        [ new_text_box_for_int (\%pr::win, 'addf_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'addf_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_accounts() {
    return new_grid_pack (3, 5, [
        [ _('Accounts window') ],
        [ new_text_box_for_int (\%pr::win, 'acco_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'acco_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Edit account window') ],
        [ new_text_box_for_int (\%pr::win, 'acce_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'acce_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_filtering() {
    return new_grid_pack (3, 11, [
        [ _('Filtering window') ],
        [ new_text_box_for_int (\%pr::win, 'filt_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'filt_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Filtering actions window') ],
        [ new_text_box_for_int (\%pr::win, 'fila_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'fila_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Filtering debug window') ],
        [ new_text_box_for_int (\%pr::win, 'fild_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'fild_h', \%HPVALUE) ],
        [ '--' ],
        [ ('Matcher window') ],
        [ new_text_box_for_int (\%pr::win, 'matc_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'matc_h', \%HPVALUE) ]

    ]);
}

sub new_winpos_subpage_useractions() {
    return new_grid_pack (3, 5, [
        [ _('User Actions prefs window') ],
        [ new_text_box_for_int (\%pr::win, 'acti_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'acti_h', \%HPVALUE) ],
        [ '--' ],
        [ _('User Actions I/O window') ],
        [ new_text_box_for_int (\%pr::win, 'acio_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'acio_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_prefs() {
    return new_grid_pack (3, 11, [
        [ _('Preferences window') ],
        [ new_text_box_for_int (\%pr::win, 'pref_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'pref_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Templates window') ],
        [ new_text_box_for_int (\%pr::win, 'temp_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'temp_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Tags window') ],
        [ new_text_box_for_int (\%pr::win, 'tags_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'tags_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Plugins window') ],
        [ new_text_box_for_int (\%pr::win, 'plug_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'plug_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_subpage_misc() {
    return new_grid_pack (4, 11, [
        [ _('Log window') ],
        [ new_text_box_for_int (\%pr::win, 'logw_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'logw_h', \%HPVALUE) ],
        [ '--' ],
        [ _('SSL manager') ],
        [ new_text_box_for_int (\%pr::win, 'sslman_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'sslman_h', \%HPVALUE) ],
        [ '--' ],
        [ _('Print preview window') ],
        [ new_text_box_for_int (\%pr::win, 'prin_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'prin_h', \%HPVALUE) ],
        [ '--' ],
        [ _('View source window') ],
        [ new_text_box_for_int (\%pr::win, 'sour_w', \%HPVALUE),
            new_text_box_for_int (\%pr::win, 'sour_h', \%HPVALUE) ]
    ]);
}

sub new_winpos_page() {
    my $winbook = Gtk3::Notebook->new;
    $winbook->set_tab_pos ('right');
    $winbook->append_page (&new_winpos_subpage_main, new_label (_('Main')));
    $winbook->append_page (&new_winpos_subpage_msgs, new_label (_('Message')));
    $winbook->append_page (&new_winpos_subpage_sendrecv, new_label (_('Send/Receive')));
    $winbook->append_page (&new_winpos_subpage_fold, new_label (_('Folder')));
    $winbook->append_page (&new_winpos_subpage_addrbook, new_label (_('Addressbook')));
    $winbook->append_page (&new_winpos_subpage_accounts, new_label (_('Accounts')));
    $winbook->append_page (&new_winpos_subpage_filtering, new_label (_('Filtering')));
    $winbook->append_page (&new_winpos_subpage_useractions, new_label (_('User Actions')));
    $winbook->append_page (&new_winpos_subpage_prefs, new_label (_('Preferences')));
    $winbook->append_page (&new_winpos_subpage_misc, new_label (_('Other')));
    return $winbook;
}

%pr::acc = ( # per account hidden preferences
    tls_set => [
        'gnutls_set_priority',
        $xl::s{l_acc_gtls_set},
        $xl::s{h_acc_gtls_set},
        'bool',
        '3.9.0.181',
        '0',
    ],
    tls_pri => [
        'gnutls_priority',
        $xl::s{l_acc_gtls_pri},
        $xl::s{h_acc_gtls_pri},
        'char,0,256,32',
        '3.9.0.181',
        '0',
    ],
);

sub new_account_subpage($) {
    my ($akey) = @_;
    return new_grid_pack (1, 3, [
        [ _('GnuTLS priority') ],
        [ new_check_button_for (\%pr::acc, 'tls_set', $ACHPVALUE{$akey}) ],
        [ new_text_box_for_nchar (\%pr::acc, 'tls_pri', $ACHPVALUE{$akey}) ]
    ]);
}

sub new_accounts_page() {
    my $accbook = Gtk3::Notebook->new;
    $accbook->set_tab_pos ('right');
    my @akeys = sort {
        $ACPREFS{$a}{'account_name'} cmp $ACPREFS{$b}{'account_name'}
    } keys %ACPREFS;
    foreach (@akeys) {
        my $name = $ACPREFS{$_}{'account_name'};
        my $isdef = ($ACPREFS{$_}{'is_default'} eq '1');
        my $page = new_account_subpage ($_);
        my $label = new_label ($isdef? '<u>' . $name . '</u>': $name);
        $label->set_use_markup (TRUE);
        $accbook->append_page ($page, $label);
    }
    $accbook->set_scrollable (TRUE);
    return $accbook;
}

%pr::plu = ( # plugins hidden preferences
    # att_remover
    arm_winw => [
        'win_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '3.9.0.74',
        '-1',
        'AttRemover',
    ],
    arm_winh => [
        'win_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '3.9.0.74',
        '-1',
        'AttRemover',
    ],
    # GPG
    gpg_alimit => [
        'autocompletion_limit',
        $xl::s{l_plu_gpg_alimit},
        $xl::s{h_plu_gpg_alimit},
        'int,0,100',
        '3.12.0.75',
        '0',
        'GPG',
    ],
    # managesieve
    msv_winw => [
        'manager_win_width',
        $xl::s{l_win_w},
        $xl::s{h_win_w},
        'int,0,3000', # 0 pixels - 3000 pixels
        '3.11.1.210',
        '-1',
        'ManageSieve',
    ],
    msv_winh => [
        'manager_win_height',
        $xl::s{l_win_h},
        $xl::s{h_win_h},
        'int,0,3000', # 0 pixels - 3000 pixels
        '3.11.1.210',
        '-1',
        'ManageSieve',
    ],
    # libravatar
    lav_burl => [
        'base_url',
        $xl::s{l_plu_lav_burl},
        $xl::s{h_plu_lav_burl},
        'char,0,1024,32',
        '3.9.3.32',
        'http://cdn.libravatar.org/avatar',
        'Libravatar',
    ],
    # perl
    prl_flvb => [
        'filter_log_verbosity',
        $xl::s{l_plu_prl_flvb},
        $xl::s{h_plu_prl_flvb},
        '0=l_plu_prl_none;1=l_plu_prl_manual;2=l_plu_prl_action;3=l_plu_prl_match',
        '3.9.0.75',
        '2',
        'PerlPlugin',
    ],
);

sub new_plugins_page() {
    my %widget = (
        'AttRemover' => [
            new_text_box_for_int (\%pr::plu, 'arm_winw', $PLHPVALUE{'AttRemover'}),
            new_text_box_for_int (\%pr::plu, 'arm_winh', $PLHPVALUE{'AttRemover'})
        ],
        'GPG' => [
            new_text_box_for_int (\%pr::plu, 'gpg_alimit', $PLHPVALUE{'GPG'})
        ],
        'ManageSieve' => [
            new_text_box_for_int (\%pr::plu, 'msv_winw', $PLHPVALUE{'ManageSieve'}),
            new_text_box_for_int (\%pr::plu, 'msv_winh', $PLHPVALUE{'ManageSieve'})
        ],
        'Libravatar' => [
            new_text_box_for_nchar (\%pr::plu, 'lav_burl', $PLHPVALUE{'Libravatar'})
        ],
        'PerlPlugin' => [
            new_selection_box_for (\%pr::plu, 'prl_flvb', $PLHPVALUE{'PerlPlugin'})
        ]
    );
    foreach my $pk (@PLUGINS) {
        foreach my $wg (@{$widget{$pk}}) {
            $wg->set_sensitive (defined $PLHPVALUE{$pk});
        }
    }
    return new_grid_pack (3, 14, [
        [ _('Attachment remover') ], $widget{'AttRemover'}, [ '--' ],
        [ _('GPG') ], $widget{'GPG'}, [ '--' ],
        [ _('Sieve manager') ], $widget{'ManageSieve'}, [ '--' ],
        [ _('Libravatar') ], $widget{'Libravatar'}, [ '--' ],
        [ _('Perl') ], $widget{'PerlPlugin'}
    ]);
}

sub new_hotkeys_list_label {
    my $renderer = Gtk3::CellRendererText->new ();
    $renderer->set_property('alignment' => 'left');
    $renderer->set_property('editable' => FALSE);
    return $renderer;
}

sub new_hotkeys_list_hotkey {
    my $renderer = Gtk3::CellRendererAccel->new ();
    $renderer->set_property ('accel-mode' => 'gtk');
    $renderer->set_property ('editable' => TRUE);
    $renderer->signal_connect ('accel-edited' => sub {
        my ($w, $path, $key, $mods, $keycode) = @_;
        my $accel = Gtk3::Accelerator->name ($key, $mods);
        my ($model, $iter) = $SELHOTKEY->get_selected ();
        $model->set($iter, C_HOTKEY, "\"$accel\"");
        my $gkey = $model->get_value ($iter, C_GROUP);
        my $akey = $model->get_value ($iter, C_ACCEL);
        my $data = $HOTKEYS->{$gkey}->{$akey};
        $data->{'key'} = "\"$accel\"";
        $data->{'enabled'} = TRUE;
    });
    $renderer->signal_connect ('accel-cleared' => sub {
        my ($w, $path) = @_;
        my ($model, $iter) = $SELHOTKEY->get_selected ();
        $model->set($iter, C_HOTKEY, "\"\"");
        my $gkey = $model->get_value ($iter, C_GROUP);
        my $akey = $model->get_value ($iter, C_ACCEL);
        my $data = $HOTKEYS->{$gkey}->{$akey};
        $data->{'key'} = "\"\"";
        $data->{'enabled'} = FALSE;
    });
    return $renderer;
}

sub row_background_color {
    my ($column, $isodd) = @_;
    my $treeview = $column->get_tree_view;
    my $stylectx = $treeview->get_style_context;
    return $isodd
        ? $stylectx->get_background_color ('normal')
        : $stylectx->get_background_color ('insensitive');
}

sub new_hotkeys_list {
    my ($gkey, $group) = @_;
    my $store = Gtk3::ListStore->new(
        qw/Glib::String Glib::String Glib::String Glib::String Glib::Boolean/);
    my $even = TRUE;
    foreach my $akey (sort keys %$group) {
        my $iter = $store->append ();
        my $hotkey = $group->{$akey}->{'key'};
        my $label = $akey;
        $label =~ s/[<>]//g; # <rrsyl> and <IMAPFolder> !?
        $store->set ($iter, C_LABEL, $label, C_HOTKEY, $hotkey,
            C_GROUP, $gkey, C_ACCEL, $akey, C_ODDITY, $even);
        $even = not $even;
    }
    my $treeview = Gtk3::TreeView->new_with_model ($store);
    # labels column
    $treeview->insert_column_with_data_func (
        0, _("Menu path"), new_hotkeys_list_label (),
        sub {
            my ($col, $renderer, $model, $iter, $data) = @_;
            $renderer->set_property (
                'markup' => '<span size="smaller">'
                            . $model->get_value ($iter, C_LABEL)
                            . '</span>');
            my $bgcol = row_background_color (
                $col, $model->get_value ($iter, C_ODDITY));
            $renderer->set_property ('cell-background-rgba' => $bgcol);
        }
    );
    # hotkeys column
    $treeview->insert_column_with_data_func (
        1, _('Hotkey'), new_hotkeys_list_hotkey (),
        sub {
            my ($col, $renderer, $model, $iter, $data) = @_;
            my $hkey = $model->get_value ($iter, C_HOTKEY);
            $hkey =~ s/\"//g;
            my ($acckey, $accmod) = Gtk3::accelerator_parse ($hkey);
            $renderer->set_property ('accel-key' => $acckey);
            $renderer->set_property ('accel-mods' => $accmod);
            my $bgcol = row_background_color (
                $col, $model->get_value ($iter, C_ODDITY));
            $renderer->set_property ('cell-background-rgba' => $bgcol);
        }
    );
    # callback for saving current selection
    my $selection = $treeview->get_selection ();
    $selection->signal_connect ('changed' => sub { $SELHOTKEY = shift });
    return $treeview;
}

sub new_hotkeys_page() {
    my $swin = Gtk3::ScrolledWindow->new ();
    my $vbox = Gtk3::VBox->new (FALSE, 5);
    foreach my $gkey (sort keys %$HOTKEYS) {
        my $group = $HOTKEYS->{$gkey};
        # group title
        my $glabel = new_title ($gkey);
        $glabel->set_padding (5, 1);
        $vbox->pack_start ($glabel, FALSE, FALSE, 0);
        # group key list
        my $keylist = new_hotkeys_list ($gkey, $group);
        $vbox->pack_start ($keylist, FALSE, FALSE, 0);
    }
    $swin->set_border_width (5);
    $swin->set_shadow_type ('none');
    $swin->set_policy ('automatic', 'always');
    $swin->add_with_viewport ($vbox);
    return $swin;
}

sub new_info_page() {
    my $v = get_toolkit_versions ();
    my $cfgv = $CONFIGDATA->{'Common'}{'config_version'};
    $cfgv //= '';
    return new_grid_pack (4, 11, [
        [ _('Library versions') ],
        [ new_label ('Perl-GLib'), new_title ($v->{'glib'}) ],
        [ new_label (_('GLib runtime')), new_title ($v->{'glib-r'}) ],
        [ new_label (_('GLib built')), new_title ($v->{'glib-b'}) ],
        [ new_label ('Perl-GTK3'), new_title ($v->{'gtk'}) ],
        [ new_label (_('GTK3 runtime')), new_title ($v->{'gtk-r'}) ],
        [ new_label (_('GTK3 built')), new_title ($v->{'gtk-b'}) ],
        [ '--' ],
        [ _('Claws Mail versions') ],
        [ new_label (_('Binary')), new_title ($CLAWSV) ],
        [ new_label (_('Configuration')), new_title ($cfgv) ]
    ]);
}

# version info
sub get_toolkit_versions {
    my %versions = ();
    $versions{'glib'} = $Glib::VERSION;
    # version info stuff appeared in 1.040
    if ($Glib::VERSION >= 1.040) {
        $versions{'glib-b'} = join('.', Glib->GET_VERSION_INFO);
        $versions{'glib-r'} = join('.',
            &Glib::major_version, &Glib::minor_version, &Glib::micro_version);
    }
    $versions{'gtk'} = $Gtk3::VERSION;
    if ($Gtk3::VERSION >= 0.034) {
        $versions{'gtk-b'} = &Gtk3::GET_VERSION_INFO
    } else {
        $versions{'gtk-b'} = _('Not available')
    }
    $versions{'gtk-r'} = join('.',
        &Gtk3::get_major_version, &Gtk3::get_minor_version, &Gtk3::get_micro_version);
    return \%versions;
}

sub print_version() {
    print $xl::s{about_title} . "\n";
    print $xl::s{about_version} . " $VERSION\n";
    my $v = get_toolkit_versions ();
    if ($v->{'glib-b'}) {
        print _("Perl-GLib version {glibv}, built for {glibb}, running with {glibr}.",
                glibv => $v->{'glib'},
                glibb => $v->{'glib-b'},
                glibr => $v->{'glib-r'});
    } else {
        print _("Perl-GLib version {glibv}.", glibv => $v->{'glib'});
    }
    print "\n";
    if ($v->{'gtk-b'}) {
        print _("Perl-GTK3 version {gtkv}, built for {gtkb}, running with {gtkr}.",
                gtkv => $v->{'gtk'},
                gtkb => $v->{'gtk-b'},
                gtkr => $v->{'gtk-r'});
    } else {
        print _("Perl-GTK3 version {gtkv}.", gtkv => $v->{'gtk'});
    }
    print "\n";
    my $clawsver = ($CLAWSV eq "") ?
                _("Claws Mail was not found!") :
                _("Claws Mail returned version {cmv}.", cmv => $CLAWSV);
    print $clawsver . "\n";
}

# the command line help
sub print_help() {
    my $line = '-' x length ($xl::s{about_title});
    say $line;
    say $xl::s{about_title};
    say $line;
    my @help = (
        _("Syntax:"),
        _("  clawsker [options]"),
        _("Options:"),
        _("  -a|--alternate-config-dir <dir>  Uses <dir> as Claws Mail configuration."),
        _("  -b|--verbose                     More messages on standard output."),
        _("  -c|--clawsrc <file>              Uses <file> as full resource name."),
        _("  -h|--help                        Prints this help screen and exits."),
        _("  -r|--read-only                   Disables writing changes to disk."),
        _("  -v|--version                     Prints version information and exits.")
    );
    foreach (@help) { say $_ }
}

sub parse_command_line {
    my $cont = TRUE;
    $CLAWSV = get_claws_version ();
    eval {
        GetOptions('h|help' => sub { print_help (); $cont = FALSE },
            'v|version' => sub { print_version (); $cont = FALSE },
            'b|verbose' => sub { $VERBOSE = TRUE },
            'r|read-only' => sub { $READONLY = TRUE },
            'u|use-claws-version=s' => \&opt_use_claws_version,
            'a|alternate-config-dir=s' => \&opt_alternate_config_dir,
            'c|clawsrc=s' => \&opt_clawsrc)
        or die _("try -h or --help for syntax.\n");
    };
    if ($@) {
        my $msg = _("Error in options: {msg}\n", msg => $@);
        if (defined $ENV{'DISPLAY'} and $ENV{'DISPLAY'} ne '') {
            eval { Gtk3->init };
            error_dialog ($msg) unless $@;
        }
        die $msg;
    }
    return $cont;
}

sub opt_use_claws_version {
    my ($name, $value) = @_;
    die _("Error: {opt} requires a dotted numeric value argument\n", opt => $name)
        unless ($value =~ /^[\d\.]+$/);
    $CLAWSV = $value;
}

sub opt_alternate_config_dir {
    my ($name, $value) = @_;
    die _("Error: '{dir}' is not a directory or does not exist\n", dir => $value)
        unless -d $value;
    $CONFIGDIR = $value;
    $ALTCONFIGDIR = TRUE;
}

sub opt_clawsrc {
    my ($name, $value) = @_;
    die _("Error: '{value}' is not a file or does not exist\n", value => $value)
        unless -f $value;
    set_rc_filename ($value);
}

# update the hidden preferences status from loaded values
sub init_hidden_preferences {
    foreach my $hash (\%pr::beh, \%pr::col, \%pr::gui, \%pr::oth, \%pr::win) {
        foreach my $key (keys %$hash) {
            $HPVALUE{${$hash}{$key}[NAME]}[VALUE] = $PREFS{${$hash}{$key}[NAME]};
            $HPVALUE{${$hash}{$key}[NAME]}[IVALUE] = $PREFS{${$hash}{$key}[NAME]};
        }
    }
    foreach my $akey (keys %ACPREFS) {
        foreach my $key (keys %pr::acc) {
            my $pname = $pr::acc{$key}[NAME];
            $ACHPVALUE{$akey}{$pname}[VALUE] = $ACPREFS{$akey}{$pname};
            $ACHPVALUE{$akey}{$pname}[IVALUE] = $ACPREFS{$akey}{$pname};
        }
    }
    foreach my $key (keys %pr::plu) {
        my $plugin = $pr::plu{$key}[PLUGIN];
        my $pname = $pr::plu{$key}[NAME];
        if (defined $PLPREFS{$plugin}) {
            $PLHPVALUE{$plugin}{$pname}[VALUE] = $PLPREFS{$plugin}{$pname};
            $PLHPVALUE{$plugin}{$pname}[IVALUE] = $PLPREFS{$plugin}{$pname};
        }
    }
    return TRUE;
}

# generic load/save resource files
sub load_resource {
    my $rc = shift;
    my %data = ();
    my %meta = ();
    my $line = 0;
    open (RCF, '<:encoding(utf8)', $rc)
        or die _("Error: opening '{file}' for reading", file => $rc) . ": $!\n";
    my $section = '_'; # default unnamed section
    while (<RCF>) {
        chomp;
        ++$line;
        next if (/^\s*$/);
        if (/^\[([^\]]+)\]$/) { # new section
            $section = $1;
            die _("Error: duplicate section '{sect}' in resource file '{file}'\n",
                sect => $section, file => $rc) if ($data{$section});
            $data{$section} = {};
            $meta{$section}{'#'} = $line;
        }
        elsif (/^([0-9a-z_]+)=(.*)$/) { # key=value
            $data{$section}{$1} = $2;
            $meta{$section}{$1} = $line;
        }
        elsif (/^(.*)$/) { # lone value
            push (@{$data{$section}{'_'}}, $1);
        }
    }
    close (RCF);
    return (\%data, \%meta);
}

sub save_resource {
    my ($rc, $data, $meta) = @_;
    open (RCF, '>:utf8', $rc)
        or die _("Error: opening '{file}' for writing", file => $rc) . ": $!\n";
    my @sections = keys %$data;
    if (defined $meta) {
        @sections = sort {
            $meta->{$a}{'#'} <=> $meta->{$b}{'#'}
        } @sections
    }
    foreach my $section (@sections) {
        say RCF "[$section]";
        if (ref ($data->{$section}{'_'}) eq 'ARRAY') {
            foreach my $val (@{$data->{$section}{'_'}}) {
                say RCF $val;
            }
        } else {
            my @keys = keys %{$data->{$section}};
            if (defined $meta) {
                @keys = sort {
                    $meta->{$section}{$a} <=> $meta->{$section}{$b}
                } @keys
            }
            foreach my $key (@keys) {
                my $val = $data->{$section}{$key};
                say RCF "$key=$val";
            }
        }
        say RCF "";
    }
    close (RCF);
}

sub backup_resource {
    my $rc = shift;
    my $rcbak = "$rc.backup";
    do {
        my $emsg = _("Unable to create backup file '{name}'\n", name => $rcbak);
        log_message ($emsg);
        error_dialog ($emsg);
        return FALSE;
    } unless rename ($rc, $rcbak);
    return TRUE;
}

# specific loaders
sub load_menurc {
    my $rc = shift;
    open (RCF, '<:encoding(utf8)', $rc)
        or die _("Error: opening '{file}' for reading", file => $rc) . ": $!\n";
    my %groups = ();
    my $line = 0;
    while (<RCF>) {
        chomp;
        if (/^; \(gtk_accel_path "<([A-Za-z]+)>([^"]+)" ([^\)]+)\)$/) {
            my %data = ('key' => $3, 'enabled' => FALSE, 'line' => $line);
            $groups{$1}{$2} = \%data;
            # say "group -> $1 | path -> $2 | key -> $3";
        } elsif (/^\(gtk_accel_path "<([A-Za-z]+)>([^"]+)" ([^\)]+)\)$/) {
            my %data = ('key' => $3, 'enabled' => TRUE, 'line' => $line);
            $groups{$1}{$2} = \%data;
            # say "group -> $1 | path -> $2 | key -> $3";
        }
        ++$line;
    }
    close (RCF);
    return \%groups;
}

sub save_menurc {
    my ($rc, $groups) = @_;
    my @lines = ();
    foreach my $gkey (keys %$groups) {
        my $group = $groups->{$gkey};
        foreach my $akey (keys %$group) {
            my $data = $group->{$akey};
            my $key = $data->{'key'};
            my $line = $data->{'line'};
            $lines[$line] = ($data->{'enabled'})? '': '; ';
            $lines[$line] .= '(gtk_accel_path "<'
                    . $gkey . '>' . $akey . '" ' . $key . ')';
        }
    }
    open (RCF, '>:utf8', $rc)
        or die _("Error: opening '{file}' for writing", file => $rc) . ": $!\n";
    say RCF '; claws-mail GtkAccelMap rc-file         -*- scheme -*-';
    say RCF '; this file is an automated accelerator map dump';
    say RCF ';';
    foreach (@lines) { say RCF $_ if $_ }
    close (RCF);
}

# load current status from disc
sub load_rc_preferences {
    my $rc = get_rc_filename ();
    log_message ("Loading preferences from $rc\n");
    return FALSE unless check_rc_file ($rc);
    ($CONFIGDATA, $CONFIGMETA) = load_resource ($rc);
    foreach (keys %{$CONFIGDATA->{'Common'}}) {
        $PREFS{$_} = $CONFIGDATA->{'Common'}{$_};
    }
    foreach my $plugin (@PLUGINS) {
        if (defined $CONFIGDATA->{$plugin}) {
            push (@AVPLUGINS, $plugin);
            foreach (keys %{$CONFIGDATA->{$plugin}}) {
                $PLPREFS{$plugin}{$_} = $CONFIGDATA->{$plugin}{$_};
            }
        }
    }
    return TRUE;
}

sub load_ac_preferences {
    my $rc = get_ac_rc_filename ();
    log_message ("Loading account preferences from $rc\n");
    return FALSE unless check_rc_file ($rc);
    ($ACCOUNTDATA, $ACCOUNTMETA) = load_resource ($rc);
    foreach my $asect (keys %$ACCOUNTDATA) {
        if ($asect =~ /^Account: (\d+)$/) {
            foreach (keys %{$ACCOUNTDATA->{$asect}}) {
                $ACPREFS{$1}{$_} = $ACCOUNTDATA->{$asect}{$_};
            }
        }
    }
    return TRUE;
}

sub load_hk_preferences {
    my $rc = get_menurc_filename ();
    return FALSE unless check_rc_file ($rc);
    $HOTKEYS = load_menurc ($rc);
    return TRUE;
}

sub load_preferences {
    return FALSE unless check_claws_not_running ();
    return (load_rc_preferences ()
        and load_ac_preferences ()
        and load_hk_preferences ()
    );
}

# save current preferences to disc
sub save_rc_preferences {
    my $rc = get_rc_filename ();
    log_message ("Saving preferences to $rc\n");
    return FALSE unless check_rc_file ($rc);
    return FALSE unless backup_resource ($rc);
    foreach (keys %PREFS) {
        if (defined $HPVALUE{$_}) {
            $CONFIGDATA->{'Common'}{$_} = $HPVALUE{$_}[VALUE];
        }
    }
    foreach my $plugin (@AVPLUGINS) {
        foreach (keys %{$CONFIGDATA->{$plugin}}) {
            if (defined $PLHPVALUE{$plugin}{$_}) {
                $CONFIGDATA->{$plugin}{$_} = $PLHPVALUE{$plugin}{$_}[VALUE];
            }
        }
    }
    save_resource ($rc, $CONFIGDATA, $CONFIGMETA);
    return TRUE;
}

sub save_ac_preferences {
    my $rc = get_ac_rc_filename ();
    log_message ("Saving account preferences to $rc\n");
    return FALSE unless check_rc_file ($rc);
    return FALSE unless backup_resource ($rc);
    foreach my $asect (keys %$ACCOUNTDATA) {
        if ($asect =~ /^Account: (\d+)$/) {
            foreach (keys %{$ACCOUNTDATA->{$asect}}) {
                if (defined $ACHPVALUE{$1}{$_}) {
                    $ACCOUNTDATA->{$asect}{$_} = $ACHPVALUE{$1}{$_}[VALUE];
                }
            }
        }
    }
    save_resource ($rc, $ACCOUNTDATA, $ACCOUNTMETA);
    return TRUE;
}

sub save_hk_preferences {
    my $rc = get_menurc_filename ();
    log_message ("Saving hotkey preferences to $rc\n");
    return FALSE unless check_rc_file ($rc);
    return FALSE unless backup_resource ($rc);
    save_menurc ($rc, $HOTKEYS);
    return TRUE;
}

sub save_preferences {
    return FALSE unless check_claws_not_running ();
    my $result = save_rc_preferences ()
        and save_ac_preferences ()
        and save_hk_preferences ();
    $MODIFIED = 0 if $result;
    return $result;
}

# create notebook
sub new_notebook {
    my $nb = Gtk3::Notebook->new;

    $nb->append_page (&new_behaviour_page, Gtk3::Label->new (_('Behaviour')));
    $nb->append_page (&new_colours_page, Gtk3::Label->new (_('Colours')));
    $nb->append_page (&new_gui_page, Gtk3::Label->new (_('GUI')));
    $nb->append_page (&new_other_page, Gtk3::Label->new (_('Other')));
    $nb->append_page (&new_winpos_page, Gtk3::Label->new (_('Windows')));
    $nb->append_page (&new_accounts_page, Gtk3::Label->new (_('Accounts')));
    $nb->append_page (&new_plugins_page, Gtk3::Label->new (_('Plugins')));
    $nb->append_page (&new_hotkeys_page, Gtk3::Label->new (_('Hotkeys')));
    $nb->append_page (&new_info_page, Gtk3::Label->new (_('Info')));

    return $nb;
}

# create an about dialog
sub new_about_dialog {
    my ($parent) = @_;
    my $year = '2007-2018';
    my $holder = 'Ricardo Mones <ricardo@mones.org>';
    my $url = 'http://www.claws-mail.org/clawsker.php';
    my $icons = &get_app_icons;

    my $dialog = Gtk3::AboutDialog->new;
    $dialog->set_transient_for ($parent);
    $dialog->set_program_name ('Clawsker');
    $dialog->set_version ($VERSION);
    $dialog->set_copyright ("Copyright  $year $holder");
    $dialog->set_license_type ('gpl-3-0');
    $dialog->set_website ($url);
    $dialog->set_website_label (_("Visit Clawsker's web page"));
    # committers, by number of commits
    $dialog->set_authors ([
        $holder,
        'Tristan Chabredier (wwp) <subscript@free.fr>',
        'Andreas Rnnquist <andreas@ronnquist.net>',
        'Christian Hesse <mail@eworm.de>',
    ]);
    $dialog->set_artists ([
        'Jesper Schultz <jesper@schultz-net.dk>',
        $holder,
    ]);
    $dialog->set_documenters ([
        $holder,
        'Paul Mangan <paul@claws-mail.org>',
    ]);
    # active translators, in alphabetical order
    $dialog->set_translator_credits (join ("\n",
        'Andreas Rnnquist <andreas@ronnquist.net>',
        'Axel Kllhofer <AxelKoellhofer@web.de>',
        'David Medina <opensusecatala@gmail.com>',
        'Erik P. Olsen <erik@epo.dk>',
        'Frederico Goncalves Guimaraes <frederico@teia.bio.br>',
        'Marcel Pol <marcel@timelord.nl>',
        'Mark Chang <mark.cyj@gmail.com>',
        'M. Sulchan Darmawan <bleketux@gmail.com>',
        'Numan Demirden <if.gnu.linux@gmail.com>',
        'Pedro Albuquerque <pmra@gmx.com>',
        'Petter Adsen <petter@synth.no>',
        $holder,
        'Tristan Chabredier (wwp) <subscript@free.fr>',
    ));
    $dialog->set_title ($xl::s{about_title});
    $dialog->set_logo ($icons->[-1]);

    return $dialog;
}

sub exit_handler {
  my ($parent) = @_;
  if ($MODIFIED != 0 and not $READONLY) {
    my $markup = "<span>" . _('There are unapplied modifications.')
        . "</span>\n\n<span weight=\"bold\">"
        . _('Do you really want to quit?') . "</span>\n";
    my $dialog = message_dialog (
        $parent, _('Clawsker warning'), $markup, 'question',
        [ 'gtk-no', 1, 'gtk-yes', 0 ]
    );
    my $resp = $dialog->run;
    $dialog->hide;
    return TRUE if $resp;
  }
  Gtk3->main_quit;
}

# create buttons box
sub new_button_box {
    my ($parent, $adlg) = @_;
    my $b_about = Gtk3::Button->new_from_stock ('gtk-about');
    my $b_exit = Gtk3::Button->new_from_stock ('gtk-quit');
    my $b_apply = Gtk3::Button->new_from_stock ('gtk-apply');
    # disable button until is really implemented
    # my $b_undo = Gtk3::Button->new_from_stock ('gtk-undo');
    my $hbox = Gtk3::HBox->new (FALSE, 5);
    # signal handlers
    $b_exit->signal_connect (clicked => sub { exit_handler($parent) });
    $b_apply->set_sensitive (not $READONLY);
    $b_apply->signal_connect (clicked => sub { save_preferences($parent) });
    # $b_undo->signal_connect (clicked => sub { undo_current_changes });
    $b_about->signal_connect (clicked => sub { $adlg->run; $adlg->hide });
    # package them
    $hbox->pack_end ($b_apply, FALSE, FALSE, 0);
    $hbox->pack_end ($b_exit, FALSE, FALSE, 0);
    # $hbox->pack_end ($b_undo, FALSE, FALSE, 0);
    $hbox->pack_start ($b_about, FALSE, FALSE, 0);
    #
    return $hbox;
}

sub get_app_icons {
    return \@APPICONS if (@APPICONS);
    my @names;
    if (-d $DATADIR) { # installed
        my $dir = catdir ($DATADIR, 'icons', 'hicolor');
        @names = map {
            catfile ($dir, $_ . 'x' . $_, 'apps', $NAME . '.png')
        } (48, 64, 128);
    } else { # unpacked tarball or git clone
        @names = map {
            catfile ('.', 'icons', $NAME . '-' . $_ . '.png');
        } (48, 64, 128);
    }
    foreach (@names) {
        my $icon = undef;
        $icon = Gtk3::Gdk::Pixbuf->new_from_file($_) if (-f $_);
        push @APPICONS, $icon if ($icon);
    }
    return \@APPICONS;
}

sub escape_key_handler {
    my ($widget, $event) = @_;
    if ($event->keyval == Gtk3::Gdk::keyval_from_name('Escape')) {
        exit_handler($widget);
    }
}

# initialise
exit unless parse_command_line ();
Gtk3->init;
$main_window = Gtk3::Window->new ('toplevel');
exit unless load_preferences ();
exit unless init_hidden_preferences ();
# create main GUI
my $box = Gtk3::VBox->new (FALSE, 5);
$box->set_border_width(3);
my $about = new_about_dialog ($main_window);
$box->pack_start (new_notebook (), TRUE, TRUE, 0);
$box->pack_end (new_button_box ($main_window, $about), FALSE, FALSE, 0);
$main_window->signal_connect (delete_event => sub { exit_handler($main_window) });
$main_window->signal_connect (key_press_event => \&escape_key_handler);
$main_window->set_title (_('Claws Mail Hidden Preferences'));
$main_window->set_icon_list (get_app_icons ());
$main_window->add ($box);
$main_window->show_all;
$MODIFIED = 0;
Gtk3->main;

